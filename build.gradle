plugins {
    id 'java'
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.12.0"
    id 'maven-publish'
    id "com.jfrog.artifactory" version "4.15.2"
    id 'net.researchgate.release' version '2.8.1'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
apply plugin: 'maven-publish'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation group: 'org.apache.commons', name: 'commons-compress', version: '1.20'
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.5.0'
    implementation group: 'commons-io', name: 'commons-io', version: '2.8.0'
    implementation group: 'net.lingala.zip4j', name: 'zip4j', version: '2.6.4'
    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation 'org.projectlombok:lombok:1.18.22'
    testImplementation 'org.junit.platform:junit-platform-commons:1.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
}

gradlePlugin {
    plugins {
        dependencyManager {
            id = 'org.devopsmindset.dependency-manager'
            implementationClass = 'org.devopsmindset.gradle.DependencyManagerPlugin'
            displayName = 'Dependency Manager'
            description = 'Downloads a set of dependencies depending of a given configuration'
        }
    }
}

release {
    failOnCommitNeeded = false
    pushReleaseVersionBranch = false
    git {
        requireBranch = 'master'
    }
}

test {
    useJUnitPlatform {
        excludeTags "internal"
    }
    testLogging {
        outputs.upToDateWhen { false }
        showStandardStreams = true
    }
    systemProperties = [
            'junit.jupiter.execution.parallel.enabled'     : 'true',
            'junit.jupiter.execution.parallel.mode.default': 'concurrent'
    ]
}

def internalTest = tasks.register("InternalTest", Test) {
    group "verification"
    useJUnitPlatform {
        includeTags "internal"
    }
}

pluginBundle {
    website = 'https://wwww.devopsmindset.org'
    vcsUrl = 'https://github.com/devopsmindset/dependency-manager-gradle-plugin.git'
    tags = ['dependency', 'dependencies']
}

tasks.named("check"){
    dependsOn internalTest
}


